[gd_resource type="Shader" format=2]

[resource]
code = "// [Godot Stereo 3D]
// created by Andres Hernandez
shader_type spatial;
render_mode skip_vertex_transform, depth_test_disable,
	unshaded, shadows_disabled, ambient_light_disabled;

// separation between the eyes
uniform float stereo_strength = 0.0002;
// focus plane on z axis
uniform float parallax_depth = 0.5;
// overlap of left/right images
uniform float parallax_offset = 0.01;
// z near of the camera
uniform float z_near = 0.1;
// z far of the camera
uniform float z_far = 100.0;
// x offset for sample coordinates
const float samples[3] = {0.5, 0.66, 1.0};
// count for above samples
const int sample_count = 3;
// left color filter
const mat3 left_filter = mat3(
	vec3(0.4561, 0.500484, 0.176381),
	vec3(-0.400822, -0.0378246, -0.0157589),
	vec3(-0.0152161, -0.0205971, -0.00546856));
// right color filter
const mat3 right_filter = mat3(
	vec3(-0.0434706, -0.0879388, -0.00155529),
	vec3(0.378476, 0.73364, -0.0184503),
	vec3(-0.0721527, -0.112961, 1.2264));

// scales the overlay to full screen
void vertex() {
	POSITION = vec4(VERTEX, 1.0);
}

// samples the color from screen buffer
vec3 getColor(sampler2D tex, vec2 uv) {
	vec3 color = textureLod(tex, uv, 0.0).rgb;
	return color;
}

// samples the depth from depth buffer in linear space
float getDepth(sampler2D tex, vec2 uv) {
	float depth_sample = textureLod(tex, uv, 0.0).r;
	float ndc = depth_sample * 2.0 - 1.0;
	float linear_depth = (2.0 * z_near * z_far) / (z_far + z_near - ndc * (z_far - z_near));
	linear_depth /= z_far;
	return linear_depth;
}

// get the parallax offset
float getParallax(float depth) {
	depth = 1.0 - parallax_depth / depth;
	return depth;
}

// stereo 3D algorithm samples buffers and finds approximate depth to distort left/right images
void fragment() {
	float min_depth_left = 1.0;
	float min_depth_right = 1.0;
	vec2 uv = vec2(0.0);
	
	for (int i = 0; i < sample_count; ++i) {
		uv.x = samples[i] * stereo_strength + parallax_offset;
		min_depth_left = min(min_depth_left, getDepth(DEPTH_TEXTURE, SCREEN_UV + uv));
		min_depth_right = min(min_depth_right, getDepth(DEPTH_TEXTURE, SCREEN_UV - uv));
	}
	
	float parallax_left = stereo_strength * getParallax(min_depth_left) + parallax_offset;
	float parallax_right = stereo_strength * getParallax(min_depth_right) + parallax_offset;
	
	vec3 color_left = getColor(SCREEN_TEXTURE, SCREEN_UV + vec2(parallax_left, 0.0));
	vec3 color_right = getColor(SCREEN_TEXTURE, SCREEN_UV - vec2(parallax_right, 0.0));
	
	vec3 color_final = clamp(color_left * left_filter, vec3(0.0), vec3(1.0)) + clamp(color_right * right_filter, vec3(0.0), vec3(1.0));

	ALBEDO = color_final;
}
"
